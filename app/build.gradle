apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.compileSdkVersion
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "org.ak80.taskbuddy"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }

    sourceSets {
        final String SHARED_TEST_DIR = 'src/sharedTest/java'
        test {
            java.srcDirs += SHARED_TEST_DIR
        }
        androidTest {
            java.srcDirs += SHARED_TEST_DIR
        }
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    }
}

dependencies {
    // App's dependencies, including test
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.androidConstraintLayoutVersion"
    implementation "org.jetbrains.anko:anko-commons:$rootProject.ankoVersion"
    implementation "org.jetbrains.anko:anko-support-v4-commons:$rootProject.ankoVersion"
    implementation "org.jetbrains.anko:anko-sqlite:$rootProject.ankoVersion"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    // Dagger dependencies
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.assertj:assertj-core:$rootProject.assertjVersion"
    testImplementation "io.mockk:mockk:$rootProject.mockkVersion"
    testImplementation "com.tngtech.archunit:archunit:$rootProject.archunitVersion"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$rootProject.androidTestRunnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.androidTestRulesVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.androidEspressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.androidEspressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.androidEspressoVersion"
    androidTestImplementation "com.android.support:support-annotations:$rootProject.androidSupportAnnotationsVersion"

    // Resolve conflicts between prod and test
    androidTestImplementation "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestImplementation "com.android.support:design:$rootProject.supportLibraryVersion"

}